[%
	import 'util.eol';
	
	var t : Template = TemplateFactory.load("class.egl");
	var outputRoot = t.getOutputRoot(); 
	var packageName = t.getBasePackage();
	
	TemplateFactory.setOutputRoot(outputRoot);

    for (class in EClass.allInstances()) { 
      t.createClass(packageName, class.filename(), class);
      if (class.hasVaultAnnotation()){
      	// create the vault class
      	t.createBaseClass(packageName, class.baseFilename(), class);
      	
      	// create all handlers for all its public operations
      	for (eOperation in class.getPublicEOperations()){
      		t.createHandler(packageName, eOperation);
      	} 
      }
    }

operation Template getJavaSourcePath(): String{
	return "../src/main/java/";
}

operation Template getBasePackage(): String{
	var eAnnotation = EAnnotation.allInstances.selectOne(a | a.source == "http://www.eclipse.org/emf/2002/GenModel");
	return eAnnotation.details.selectOne( d | d.key = "basePackage").value;
}

operation Template getOutputRoot() : String {
	return  self.getJavaSourcePath() + self.getBasePackage().replace("\\.", "/"); 
}

operation Template createClass(packageName : String, filename : String, class : EClass) {
    var t : Template = TemplateFactory.load("class.egl");
	
	t.populate("packageName", packageName);    
    t.populate("class", class);
    
    t.generate(filename);
  }
  
  operation Template createBaseClass(packageName : String, baseFilename : String, class : EClass) {
    var t : Template = TemplateFactory.load("baseclass.egl");
	
	t.populate("packageName", packageName);    
    t.populate("class", class);
    
    t.generate(baseFilename);
  }
  
  operation Template createHandler(packageName : String, eOperation : EOperation) {
    var t : Template;
   
    t = TemplateFactory.load("requesthandler.egl");
	t.populate("packageName", packageName);    
    t.populate("eOperation", eOperation);
    t.generate(eOperation.getClassRequestHandlerName() + ".java");
    
    t = TemplateFactory.load("responsehandler.egl");
	t.populate("packageName", packageName);    
    t.populate("eOperation", eOperation);
    t.generate(eOperation.getClassResponseHandlerName() + ".java");
  }
  
  
  
%]